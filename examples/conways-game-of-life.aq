#!/usr/local/bin/aq

Run '../prelude/stringfunctions.aq';

# set the size of your console window here
width := 80 - 1;
height := 23;

# set the command for clearing the screen here
clearscreen := ESC & '[2J';

pad := \s : String ->
    If length(s) >= width: s
    Else: s & space(width - length(s))
    EndIf;

board := {};
board[0] := pad('    ');
board[1] := pad('  * ');
board[2] := pad('   *');
board[3] := pad(' ***');
For y From 4 To height - 1:
    board[y] := space(width);
EndFor

iterate := \-> {
    newboard := {};
    For y From 0 To height - 1:
        newboard[y] := '';
    EndFor
    For y From 0 To height - 1:
        For x From 0 To width - 1:
            live := 0;
            For ry From -1 To 1:
                For rx From -1 To 1:
                    If rx <> 0 or ry <> 0:
                        ax := x + rx;
                        ay := y + ry;
                        If 0 <= ax and ax < width and
                           0 <= ay and ay < height:
                            neighbor := charat(board[ay], ax);
                            If neighbor eq '*':
                                live := live + 1;
                            ElseIf neighbor ne ' ':
                                Call error('Illegal neighbor encountered: ' & neighbor);
                            EndIf
                        EndIf
                    EndIf
                EndFor
            EndFor
            self := charat(board[y], x);
            If self eq ' ':
                If live = 3:
                    self := '*';
                EndIf
            ElseIf self eq '*':
                If live < 2 or live > 3:
                    self := ' ';
                EndIf
            Else:
                Call error('Illegal self encountered: ' & self);
            EndIf
            newboard[y] := newboard[y] & self;
        EndFor
    EndFor
    For y From 0 To height - 1:
        board[y] := newboard[y];
    EndFor
};

print := \-> {
    Write clearscreen;
    For y From 0 To height - 1 (
        Write board[y] & '|';
    EndFor
};

For i From 1 To 50:
    Call print();
    Call iterate();
EndFor
Call print();
